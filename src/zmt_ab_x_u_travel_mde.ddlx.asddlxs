@Metadata.layer: #CORE
@UI.headerInfo: { 
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: {  value: 'TravelId' },
    description: { value : 'Memo' }
}
annotate entity ZMT_AB_X_U_TRAVEL
    with 
{
    @UI.facet: [
                { 
                    id: 'TravelHeaderPrice',
                    purpose: #HEADER,
                    type: #DATAPOINT_REFERENCE,
                    position: 10,
                    targetQualifier: 'PriceData'
                },{ 
                    id: 'TravelHeaderStatus',
                    purpose: #HEADER,
                    type: #DATAPOINT_REFERENCE,
                    position: 20,
                    targetQualifier: 'StatusData'
                },
                { 
                    purpose: #STANDARD,
                    type: #COLLECTION,
                    label: 'More Details',
                    id: 'super'
                },
                { 
                    purpose: #STANDARD,
                    type: #IDENTIFICATION_REFERENCE,
                    label: 'More Info',
                    parentId: 'super',
                    position: 10
                },
                { 
                    purpose: #STANDARD,
                    type: #FIELDGROUP_REFERENCE,
                    label: 'Dates',
                    parentId: 'super',
                    position: 20,
                    targetQualifier: 'dates'
                },
                { 
                    purpose: #STANDARD,
                    type: #FIELDGROUP_REFERENCE,
                    label: 'Price Info',
                    parentId: 'super',
                    position: 30,
                    targetQualifier: 'prices'
                } 
    ]
    @UI.selectionField: [{ position: 10 }]
    @UI.lineItem: [{ position: 10 },
    { type: #FOR_ACTION, label: 'Set Booking status', dataAction: 'set_booked_status' }]
    @UI.identification: [{ position: 10 }]
    TravelId;
    @UI.selectionField: [{ position: 20 }]
    @UI.lineItem: [{ position: 20 }]
    @UI.identification: [{ position: 20 }]
    AgencyId;
    @UI.selectionField: [{ position: 30 }]
    @UI.lineItem: [{ position: 30 }]
    @UI.identification: [{ position: 30 }]
    CustomerId;
    @UI.selectionField: [{ position: 40 }]
    @UI.lineItem: [{ position: 40 }]
    @UI.fieldGroup: [{ position: 10, qualifier: 'dates' }]
    BeginDate;
    @UI.fieldGroup: [{ position: 20, qualifier: 'dates' }]
    EndDate;
    @UI.fieldGroup: [{ position: 10, qualifier: 'prices' }]
    BookingFee;
    @UI.selectionField: [{ position: 50 }]
    @UI.lineItem: [{ position: 50 }]
    @UI.fieldGroup: [{ position: 20, qualifier: 'prices' }]
    @UI.dataPoint: {  qualifier: 'PriceData', title: 'Price Data' }
    TotalPrice;
    @UI.fieldGroup: [{ position: 30, qualifier: 'prices' }]
    CurrencyCode;
    @UI.selectionField: [{ position: 11 }]
    Memo;
    @UI.selectionField: [{ position: 60 }]
    @UI.lineItem: [{ position: 60, importance: #HIGH }]
    @UI.fieldGroup: [{ position: 30, qualifier: 'dates' }]
    @UI.dataPoint: {  qualifier: 'StatusData', title: 'Overall Status' }
    Status;
    
}